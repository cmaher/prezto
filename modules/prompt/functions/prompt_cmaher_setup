#
# A theme that displays basic information.
#
# Authors:
#   Brian Tse <briankftse@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/zLZNK.png
#

function +vi-git_status {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]='%F{red}o%f'
  fi
}

function prompt_cmaher_precmd {
  vcs_info
}

function prompt_cmaher_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_cmaher_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr '%F{green}o%f'
  zstyle ':vcs_info:*' unstagedstr '%F{yellow}o%f'
  zstyle ':vcs_info:*' formats "(%b|%c%u)"
  zstyle ':vcs_info:*' actionformats "(%b|%c%u|%F{magenta}%a%f)"
  zstyle ':vcs_info:git*+set-message:*' hooks git_status

  # set editor-info parameters
  zstyle ':prezto:module:editor:info:keymap:primary' format '»'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '»'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '«'

  # Define prompts.
  PROMPT='%F{cyan}${PWD##*/}%f ${vcs_info_msg_0_} %F{red}${editor_info[keymap]}%f '
  RPROMPT=''
  SPROMPT=''
}

prompt_cmaher_setup "$@"
